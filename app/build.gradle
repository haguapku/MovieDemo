apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.moviedemo"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.example.moviedemo.util.MovieTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            //API KEY
            buildConfigField "String", "API_KEY", "\"6647f3438acfd9b4a26662c28298a619\""
        }
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
        main.res.srcDirs += 'src/main/resources'
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.supportLayoutVersion"

    //Support Material Design
    implementation "com.android.support:design:$rootProject.materialDesignVersion"

    //Support libraries, RecyclerView and CardView
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    // Architecture components: ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$rootProject.androidLifecycleVersion"
    testImplementation "android.arch.core:core-testing:$rootProject.androidLifecycleVersion"

    //Retrofit2 & okhttp3
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.gsonConverterVersion"
    implementation "com.squareup.retrofit2:converter-simplexml:$rootProject.simplexmlConverterVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.rxjava2AdapterVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$rootProject.okhttpMockWebServerVersion"

    //RxJava2
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxjavaAndroidVersion"

    //Dagger
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    // Dagger android
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    // Image library, Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoCoreVersion"
    androidTestImplementation "org.mockito:mockito-android:$rootProject.mockitoAndroidVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.androidTestVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.androidTestVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
}
